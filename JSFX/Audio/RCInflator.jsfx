/*
JSFX Name: RCInflator
Author: RCJacH
Release Date: Aug 2021
Link:
  https://github.com/RCJacH/ReaScripts
  https://forum.cockos.com/showthread.php?t=256286
Version: 0.4
Reference:
  tviler
  Sony Oxford
About:
  JSFX implementation of Sony Oxford Inflator algorithm,
  found on Gearspace.
Changelog:
  * v0.4 (2021-08-13)
    + Accurate decibel to float conversion.
    + Wrap waveshaper above 0dB.
    + Refactor waveshaper into function.
  * v0.3 (2021-08-11)
    + Apply waveshaper symmetrically.
    * Fix sign Error.
  * v0.2 (2021-08-09)
    * Cleaned up code.
  * v0.1 (2021-02-06)
    + Initial alpha release.
*/

desc:RCInflator

slider1:0<-60, 60, 0.1>Input (dB)
slider2:100<0, 100, 0.1>Effect (%)
slider3:curve=0<-50, 50, 0.0001>Curve
slider4:clip=0<0,1,1{Off,On}>Clip
slider5:0<-60, 6, 0.1>Output (dB)

@init

@slider
in_db = exp(0.11512925464970229 * slider1);
wet = slider2 * 0.01;
dry = 1 - wet;
out_db = exp(0.11512925464970229 * slider5);

curvepct = curve * 0.01;
// 1 + (curve + 50) / 100
curveA = 1.5 + curvepct;
// - curve / 50
curveB = -(curvepct + curvepct);
// (curve - 50) / 100
curveC = curvepct - 0.5;
// 1 / 16 - curve / 400 + curve ^ 2 / (4 * 10 ^ 4)
curveD = 0.0625 - curve * 0.0025 + (curve * curve) * 0.000025;

@sample
function apply_waveshaper(in)
local(s0, s0_2, s0_3, sgn, frac)
global(curveA, curveB, curveC, curveD,
       clip, dry, in_db, out_db, wet)
(
  s0 = abs(in);
  s0 *= in_db;
  clip ? (
    s0 = s0 > 1 ? 1 : s0;
    sgn = sign(in);
  ) : (
    s0 = 0.5 * s0 - 0.5;
    frac = s0 - floor(s0);
    s0 = 2 * abs(0.5 - frac);
    frac = in * 0.25 - 0.5;
    sgn = sign(frac - floor(frac) - 0.5);
  );
  s0_2 = s0 * s0;
  s0_3 = s0_2 * s0;
  s0 = curveA * s0 + curveB * s0_2 + curveC * s0_3 - curveD * (s0_2 - 2 * s0_3 + s0_2 * s0_2);
  s0 *= out_db;
  
  sgn * s0 * wet + in * dry
);

spl0 = apply_waveshaper(spl0);
spl1 = apply_waveshaper(spl1);
