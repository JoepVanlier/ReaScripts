/*
JSFX Name: Midi CC Eater
Author: RCJacH
Release Date: 18.02.2015
Link: https://github.com/RCJacH/ReaScripts
Version: 1.0
Reference: MIDI Eater
About:
  This JS plugin filters all inputs of selected CC.
  Instruction:
  1. Add as an input FX.
  2. Select specific CC or all CCs to filter.
  3. Win.
*/

// Licensed under the GNU GPL - http://www.gnu.org/licenses/gpl.html

desc:MIDI CC Eater
slider1:0<0,128,1{0 All,Pitch Wheel,1 Mod Wheel M,2 Breath M,3,4 Foot P M,5 Porta M,6 Data Entry M,7 Vol M,8 Balance M,9,10 Pan M,11 Expression M,12 Ctrl 1 M,13 Ctrl 2 M,14,15,16 GP Slider 1,17 GP Slider 2,18 GP Slider 3,19 GP Slider 4,20,21,22,23,24,25,26,27,28,29,30,31,32 Bank Sel L,33 Mod Wheel L,34 Breath L,35,36 Foot P L,37 Porta L,38 Data Entry L,39 Vol L,40 Balance L,41,42 Pan L,43 Expression L,44 Ctrl 1 L,45 Ctrl 2 L,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64 Hold P sw,65 Porta sw,66 Sustenuto sw,67 Soft P sw,68 Legato P sw,69 Hold 2 P sw,70 S.Variation,71 S.Timbre,72 Release,73 Attack,74 Cutoff,75 Decay,76 S.Ctrl 7,77 S.Ctrl 8,78 S.Ctrl 9,79 S.Ctrl 10,80 GP B.1 sw,81 GP B.2 sw,82 GP B.3 sw,83 GP B.4 sw,84,85,86,87,88,89,90,91 Effects Lv,92 Trem Lv,93 Chorus Lv,94 Celeste Lv,95 Phaser Lv,96 Data B. Inc,97 Data B. Dec,98 NRP L,99 NRP M,100 RP L,101 RP M,102,103,104,105,106,107,108,109,110,111,112,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127}>CC
in_pin:none
out_pin:none

@init
  STATUS_CC = $xB0;
  STATUS_PW = $xE0;

@slider
  CC_select = slider1;

@block
while(midirecv(mpos, msg1, msg23)) (
  // get message components
  in_type = msg1 & $xF0;
  in_CCN = msg23 & $xff; //Get incoming CC number
  eat = 0;  
  (in_type == STATUS_PW) && (CC_select <= 1) ? eat = 1;

  (in_type == STATUS_CC) ? (
    (CC_select == 0) || (in_CCN == CC_select - 1) ? eat = 1;
  ); //in_type
  !eat ? midisend(mpos, msg1, msg23);
);
